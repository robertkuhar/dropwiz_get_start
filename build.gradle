apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

description = 'Dropwizard Getting Started project in gradle'
version = '1.1'

ext.mainClassName = 'com.example.helloworld.HelloWorldService'

ext.dropwizard = [version: '0.7.1']

defaultTasks 'clean', 'build'

repositories { 
  mavenCentral()
}

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizard.version"
}

jar {
  manifest {
    attributes "Implementation-Title": "$description"
    attributes "Implementation-Version": version
    attributes "Main-Class" : mainClassName
  }
 
  // remove the security files (from mail.jar / activation.jar) so that the jar will be executable.
  doFirst {
    from ( configurations.runtime.resolve().collect { it.isDirectory() ? it : zipTree(it) } ) {
      exclude 'META-INF/MANIFEST.MF'
      exclude 'META-INF/*.SF'
      exclude 'META-INF/*.DSA'
      exclude 'META-INF/*.RSA'
    }
  }
}
